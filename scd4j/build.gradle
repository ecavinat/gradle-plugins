plugins {
	id "com.jfrog.bintray" version "1.3.1"
	id "nebula.os-package" version "2.2.6"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'maven'

group = 'com.datamaio'
sourceCompatibility = 1.8
targetCompatibility = 1.8
version = buildVersion()

jar {
    manifest {
        attributes 'Implementation-Title': 'SCD4J', 'Implementation-Version': version
    }
}

project.task('sourceJar', type:Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
project.artifacts {
	archives sourceJar
}

repositories {
    mavenCentral()
    maven { url "https://repository.sonatype.org/content/groups/forge/"	}
}

dependencies {
	compile gradleApi(),localGroovy()
	compile 'org.jasypt:jasypt:1.9.2', 
			'org.codehaus.groovy:groovy-all:2.4.4',
			'commons-configuration:commons-configuration:+',
			// other template engines
			'com.github.spullara.mustache.java:compiler:+',
			'com.github.jknack:handlebars:+',
			'org.apache.velocity:velocity:+'
			
    testCompile 'junit:junit:4.+',
				'org.mockito:mockito-core:2.0.36-beta'
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url : mavenLocal().url)
		}
	}
}

wrapper {
  	gradleVersion = '2.13'
}

// ---------- automatically updates gradle version in scd4j plugin according to what is defined in this project ------- 

def scd4jPluginPath = 'src/main/groovy/com/datamaio/scd4j/gradle/Scd4jPlugin.groovy'
def bkpFolder = 'build/tmp/bkpFolder'

compileJava.doFirst {
    copy {
        from(scd4jPluginPath)
        into(bkpFolder)
    } 
    ant.replace(file: scd4jPluginPath, token: '@GRADLE_VERSION@', value: project.wrapper.gradleVersion)
}

compileGroovy.doLast {
	def f = file(scd4jPluginPath)
    copy {
        from("${bkpFolder}/${f.name}")
        into(f.parent)
    }
}

//--------- publish to bintray config -----------

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':bintrayUpload')) { 
		version = buildVersion(1)
		println "************************************************"
		println "**** Publishing version '$version' to Bintray ****"
		println "************************************************"
    }
}

bintray {
	user = project.hasProperty('bintray_user') ? bintray_user : ''
	key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''
	configurations = ['archives']
	publish = true
	pkg {
		repo = 'maven'
		name = 'SCD4J'
		desc = 'Simple Continuous Delivery for Java and Groovy Developers'
		websiteUrl = 'https://github.com/scd4j/gradle-plugins/wiki'
		issueTrackerUrl = 'https://github.com/scd4j/gradle-plugins/issues'
		vcsUrl = 'https://github.com/scd4j/gradle-plugins.git'
		licenses = ['MIT']
		labels = ['Continuous Delivery', 'Java', 'Groovy', 'Gradle', 'DevOps', 'Automation', 'Linux', 'Windows', 'Debian', 'Red Hat', 'Ubuntu', 'CentOs']
		publicDownloadNumbers = true
		attributes= ['gradle-plugin': 'com.datamaio.scd4j:com.datamaio:scd4j']
				
		version {
			desc = "Simple Continuous Delivery for Java and Groovy Developers - version $project.version"
			released  = new java.util.Date()
			attributes = ['gradle-plugin': 'com.datamaio.scd4j:com.datamaio:scd4j']
		}
	}
}

//--------- Utility --------

def buildVersion(increment) {
    def f = file('version.properties') 
    if (f.canRead()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(f))
        def deployNumber = props['DEPLOY_NUMBER'].toInteger()
        if(increment) {
        	deployNumber++;
        	props['DEPLOY_NUMBER'] = deployNumber.toString()
        	props.store(f.newWriter(), null)
        }
        
        return  "${props['VERSION_NAME']}.${deployNumber}"
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}


//--------- Create DEB and RPM packages --------

ospackage {
	packageName = 'foo'
	version = '0.1'
	release = 1
	arch = I386
	os = LINUX

	preInstall "echo '---> preInstall'"
	postInstall "echo '---> postInstall'"
	preUninstall "echo '---> preUninstall'"
	postUninstall "echo '---> postUninstall'"

	into '/opt/foo'

	from ('src/ospackage/resources') {
		include '**/*'
	}
}
